######################################################################
# Generated by Zoer, 2017.11.26
######################################################################

# Используемые Qt модули
QT += widgets gui core network websockets opengl

# На выходе приложение, не библиотека
TEMPLATE = app

# Имя выходного файла
TARGET = EasyContest

# Выходная директория
unix {
  CONFIG(release, debug|release): DESTDIR = bin/release    # release
  CONFIG(debug  , debug|release): DESTDIR = bin/debug      # debug
}

# Ворнинги при использовании устаревших Qt функций
DEFINES += QT_DEPRECATED_WARNINGS

# Директории для временных файлов
MOC_DIR     = tmp
OBJECTS_DIR = tmp
RCC_DIR     = tmp
UI_DIR      = tmp

# Разрешить синтаксис С++11
CONFIG += c++11

# INCLUDEPATH += .

##### Input #####

SOURCES += \
	source/main.cpp \
	source/gui/mainwindow.cpp \
    source/logging_categories.cpp 

HEADERS += \ 
	source/gui/mainwindow.h \
    source/logging_categories.h

FORMS += \
	source/gui/mainwindow.ui

include(libs/TciClient/TciClient.pri)
include(libs/QCustomPlot/QCustomPlot.pri)
include(libs/QGlobalShortcut/qglobalshortcut.pri)

# Подключить файлы ресурсов
RESOURCES += resource/resource.qrc translations/resource.qrc

# Локализация
TRANSLATIONS += translations/EasyContest_ru.ts
tr.commands = lupdate \"$$_PRO_FILE_\" && lrelease \"$$_PRO_FILE_\"
    PRE_TARGETDEPS += tr
    QMAKE_EXTRA_TARGETS += tr

